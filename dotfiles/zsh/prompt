# git prompt
function parse_git_dirty {
  [[ $(git status 2> /dev/null | tail -n1) != "nothing to commit, working tree clean" ]] && echo "*"
}
function parse_git_branch {
  #                                   his is where the prompt begins -> space[branch*]
  git branch --no-color 2> /dev/null | sed -e '/^[^*]/d' -e "s/* \(.*\)/ [\1$(parse_git_dirty)]/"
}

# perform parameter expansion, cmd substitution, arithmetic expansion in prompt
setopt PROMPT_SUBST

# prompt
if [[ $UID -eq 0 ]]; then
  local user_host='%n@%m'
  local user_symbol='#'
else
  local user_host=$(print -P '%n@%m')
  if [ "$user_host" = "tb@W50020335L" ]; then
    local user_host="mloz"
  fi
  local user_symbol='$'
fi

local current_dir="%~"

local git_status='$(parse_git_branch)'

# k8s
local kube_status=''
local kube_ps1_src=$HOME/.kube-ps1/kube-ps1.sh

if [ -f "$kube_ps1_src" ] && which kubectl &> /dev/null; then
  KUBE_PS1_PREFIX=" ["
  KUBE_PS1_SUFFIX="]"
  KUBE_PS1_SYMBOL_PADDING=false
  KUBE_PS1_SYMBOL_DEFAULT="k8s"
  KUBE_PS1_SEPARATOR=":"
  KUBE_PS1_DIVIDER="|"
  source $kube_ps1_src
  local kube_status='$(kube_ps1)'
fi  

# terraform
function tf_prompt_info() {
    # dont show 'default' workspace in home dir
    [[ "$PWD" == ~ ]] && return
    # check if in terraform dir
    if [ -d .terraform ]; then
      workspace=$(terraform workspace show 2> /dev/null) || return
      echo " [%F{blue}tf%f:%F{red}${workspace}%f]"
    fi
}
local tf_status='$(tf_prompt_info)'

# python
export VIRTUAL_ENV_DISABLE_PROMPT=1
virtualenv_prompt() {
    # If not in a virtualenv, print nothing
    [[ "$VIRTUAL_ENV" == "" ]] && return

    # Distinguish between the shell where the virtualenv was activated
    # and its children
    local venv_name="${VIRTUAL_ENV##*/}"
    if typeset -f deactivate >/dev/null; then
        echo "[${venv_name}] "
    else
        echo "<${venv_name}> "
    fi
}
local venv_status='$(virtualenv_prompt)'


PROMPT="╭─%F{yellow}${user_host}%f %F{green}${current_dir}%f%F{blue}${git_status}%f${kube_status}${tf_status}${venv_status}
╰─%B${user_symbol}%b "
